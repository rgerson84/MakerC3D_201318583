package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER, MAYOR,MENOR,MENOR_IGUAL,MAYOR_IGUAL,IGUAL_IGUAL,NO_IGUAL,OR,AND,NEG;
nonterminal NodoC3D inicial, expresion, cond, rel, oprel;

precedence left OR;
precedence left AND;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} cond:exp 
                    {: 
                        RESULT = exp;
                        ControlC3D.agregarC3D("Etiqueta F: " + exp.getCad()+"\n");
                        ControlC3D.agregarC3D("Etiqueta V: " + exp.getCad2());
                    :};

cond        ::=     cond:izq OR {:ControlC3D.agregarC3D(izq.getCad()+":\n");:} cond:der
                    {:
                        String c3d =der.getCad()+ ":\n";
                        c3d = izq.getCad2()+","+der.getCad2()+":\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(der.getCad(),izq.getCad2()+","+der.getCad2());
                    :}

                |   cond:izq  AND {:ControlC3D.agregarC3D(izq.getCad2()+":\n");:} cond:der

                    {:
                        String c3d =der.getCad2()+ ":\n";
                        c3d = izq.getCad()+","+der.getCad()+":\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(izq.getCad()+","+der.getCad(),der.getCad2());
                    :}
                | NEG cond:c
                    {:
                        System.out.println("llego not");
                        RESULT = new NodoC3D(c.getCad2(),c.getCad());
                    :}
                |   rel:r
                    {:  
                        System.out.println("Entro Relacional");
                        RESULT = r;
                    :};

rel         ::= expresion:izq oprel:op expresion:der
                {:
                    String lv = ControlC3D.generaEtq();
                    String lf = ControlC3D.generaEtq();
                    String c3d ="if "+izq.getCad() +" "+op.getCad()+" "+der.getCad() +" go to "+lv+";\n";
                    c3d +="go to "+lf+";\n";
                    //System.out.println(op.getCad());
                    ControlC3D.agregarC3D(c3d);
                    RESULT = new NodoC3D(lf,lv);
                :}
                | expresion:e
                {:
                    RESULT = e;
                :};
                

oprel       ::= MAYOR
                {:
                    RESULT = new NodoC3D(">");
                :}
                |MENOR
                {:
                    RESULT = new NodoC3D("<");
                :}
                |MENOR_IGUAL
                {:
                    RESULT = new NodoC3D("<=");
                :}
                |MAYOR_IGUAL
                {:
                    RESULT = new NodoC3D(">=");
                :}
                |NO_IGUAL
                {:
                    RESULT = new NodoC3D("!=");
                :}
                |IGUAL_IGUAL
                {:
                    RESULT = new NodoC3D("==");
                :};

expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   PAR_IZQ cond:exp PAR_DER   
                    {: RESULT = exp; :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;